// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
    And16(a=x, b=false, out=zxr);
    And16(a=y, b=false, out=zyr);
    Not16(in=x, out=nxr);
    Not16(in=y, out=nyr);

    Mux4Way16(a=x, b=zxr, c=nxr, d=true, sel[0]=zx, sel[1]=nx, out=xr);
    Mux4Way16(a=y, b=zyr, c=nyr, d=true, sel[0]=zy, sel[1]=ny, out=yr);

    Add16(a=xr, b=yr, out=xyr);
    And16(a=xr, b=yr, out=xya);
    Not16(in=xyr, out=xyrn);
    Not16(in=xya, out=xyan);

    Mux4Way16(a=xya, b=xyr, c=xyan, d=xyrn, sel[0]=f, sel[1]=no, out=out, out=zrcheck, out[15]=ngcheck);

    And(a=ngcheck, b=true, out=ng);

    Or16Way(in=zrcheck, out=zrcheck2);
    Not(in=zrcheck2, out=zr);
}